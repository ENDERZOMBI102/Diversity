plugins {
	id 'org.quiltmc.loom' version '0.12-SNAPSHOT'
}

version = project.mod_version
group = project.maven_group

repositories {
	maven { url 'https://maven.shedaniel.me/' }
	maven { url 'https://maven.terraformersmc.com/' }
	maven { url 'https://maven.quiltmc.org/repository/release' }
}

dependencies {
	minecraft "com.mojang:minecraft:$minecraft_version"
	mappings(loom.layered {
		addLayer(quiltMappings.mappings("org.quiltmc:quilt-mappings:$minecraft_version+build.$quilt_mappings:v2"))
	})
	modImplementation "org.quiltmc:quilt-loader:$loader_version"
	modImplementation "org.quiltmc:qsl:$qsl_version+$minecraft_version"

	/* mod dependencies */

	// cloth config
	modImplementation("me.shedaniel.cloth:cloth-config-fabric:$clothconfig_version") {
		exclude group: 'net.fabricmc.fabric-api'
	}
	// modmenu
	modImplementation "com.terraformersmc:modmenu:$modmenu_version"

	/* lib dependencies */
}

processResources {
	inputs.property 'mod_version', project.version
	inputs.property 'cc_version', project.clothconfig_version
	filteringCharset "UTF-8"

	filesMatching('quilt.mod.json') {
		expand([
			'mod_version': project.version,
			'cc_version': project.clothconfig_version,
		])
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = 'UTF-8'
	it.options.release.set(17)
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
	archivesBaseName = project.archives_base_name
	withSourcesJar()
}

jar {
	from('LICENSE') {
		rename { "${it}_$archivesBaseName" }
	}
}
